SNAKE
not protected

.SNAKEGME


Lbl SNAKE
.COORDINATES, IN X,Y PAIRS
Fill(&L1,768,127)

.LENGTH
5->N

.START POSITION
For(I,0,N-1)
48-I->{I*2+&L1}
32->{I*2+1+&L1}
End

.X,Y SPEED
1->X
0->Y

.RANDOM DOT
sub(DOT)


.FOREVER
Lbl LOOP

.KEYS
getKeys
If getKey(41)
Return
End
If getKey(3)
1->X
0->Y
End
If getKey(2)
&-1->X
0->Y
End
If getKey(1)
0->X
1->Y
End
If getKey(4)
0->X
&-1->Y
End

.DRAW
ClrDraw
sub(DRAW)
.DOT
Pxl-On(A,B)
DispGraph

.FORWARD
sub(MOVE)

.IF HEAD IS OFF THE SCREEN...
If {&L1}>>95??({&L1}<<0)??({&L1+1}>>63)??({&L1+1}<<0)
sub(DIE)
End


.IF HEAD IS ON A BLACK PIXEL..
If pxl-Test({&L1},{&L1+1})

.IF THAT PIXEL WAS THE DOT...
If {&L1}=A?({&L1+1}=B)
.EAT THE DOT
sub(DOT)
N+3->N
Else
.HEAD IS TOUCHING THE SNAKE
sub(DIE)
End

End


.AS LENGTH GETS LONGER, SPEED GETS FASTER
If N<128
Pause 128-N
End


Goto LOOP
Return



Lbl DRAW
For(I,0,N-1)
Pxl-On({I*2+&L1},{I*2+1+&L1})
End
Return


Lbl MOVE
.TEMP
conj(&L1,&L3,N*2)
conj(&L3,&L1+2,N*2)
{&L1+2}+X->{&L1}
{&L1+3}+Y->{&L1+1}
Return


Lbl DOT
rand^96->A
rand^64->B
Return


Lbl DIE
Text(0,0)
DrawF N>DEC
.DispGraph
Pause 6000
Return&rad